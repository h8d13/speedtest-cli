# Maintainer: Eihdran Lego <hadean-eon-dev@proton.me>

pkgname=speedtest-cli-git
pkgver=r315.gb2d73e9
pkgrel=1
pkgdesc='CLI for testing internet bandwidth using speedtest.net (py3 fork)'
arch=('any')
_repo="h8d13/speedtest-cli"
url="https://github.com/${_repo}"

license=('Apache-2.0')
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
provides=('speedtest-cli' 'speedtest')
conflicts=('speedtest-cli' 'speedtest')
source=("${pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${pkgname}"
	printf 'r%s.g%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "${srcdir}/${pkgname}"
	python -m build --wheel --no-isolation
}

package() {
	cd "${srcdir}/${pkgname}"
	# Installs the speedtest / speedtest-cli entry-point scripts from the wheel.
	python -m installer --destdir="${pkgdir}" dist/*.whl
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
	install -Dm644 speedtest-cli.1 -t "${pkgdir}/usr/share/man/man1"
}
